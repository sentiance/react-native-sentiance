plugins {
    id "com.android.library"
}

def crashDetectionProj
if (findProject(':crash-detection')) {
  crashDetectionProj = project(':crash-detection')
} else if (findProject(':react-native-sentiance_crash-detection')) {
  // Starting from RN 0.61, the @ sign is stripped from project names
  crashDetectionProj = project(':react-native-sentiance_crash-detection')
} else if (findProject(':@react-native-sentiance_crash-detection')) {
  // On RN 0.60, the @ sign is not stripped from project names
  crashDetectionProj = project(':@react-native-sentiance_crash-detection')
} else {
  throw new GradleException('Could not find the react-native-sentiance/crash-detection package, have you installed it?')
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation crashDetectionProj

    implementation 'com.facebook.react:react-native:+'
    api("com.sentiance:sdk:${rootProject.ext.sentianceVersion}@aar") { transitive = true }
    api("com.sentiance:sdk-lifestyle:${rootProject.ext.sentianceVersion}@aar") { transitive = true }
    api("com.sentiance:sdk-crash-detection:${rootProject.ext.sentianceVersion}@aar") { transitive = true }
}
