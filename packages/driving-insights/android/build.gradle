plugins {
  id "com.android.library"
}

def coreProj
if (findProject(':core')) {
  coreProj = project(':core')
} else if (findProject(':sentiance-react-native_core')) {
  // Starting from RN 0.61, the @ sign is stripped from project names
  coreProj = project(':sentiance-react-native_core')
} else if (findProject(':@sentiance-react-native_core')) {
  // On RN 0.60, the @ sign is not stripped from project names
  coreProj = project(':@sentiance-react-native_core')
} else {
  throw new GradleException('Could not find the @sentiance-react-native/core package, have you installed it?')
}

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

apply from: "$coreProj.projectDir/package-json-reader.gradle"
apply from: "$coreProj.projectDir/sentiance-version-finder.gradle"

def corePackageJson = PackageJson.of(coreProj)
applyAndroidVersionsFrom(corePackageJson)
def sentianceSdkVersion = getSentianceSdkVersion()

dependencies {
  api coreProj

  implementation(platform("com.sentiance:sdk-bom:${sentianceSdkVersion}"))
  api("com.sentiance:sdk-driving-insights") { transitive = true }

  testImplementation "junit:junit:4.13.2"
  testImplementation 'org.robolectric:robolectric:4.9'
  testImplementation "org.powermock:powermock-module-junit4:1.6.6"
  testImplementation "org.powermock:powermock-module-junit4-rule:1.6.6"
  testImplementation "org.powermock:powermock-api-mockito:1.6.6"
  testImplementation "org.powermock:powermock-classloading-xstream:1.6.6"
}

applyReactNativeDependency()
