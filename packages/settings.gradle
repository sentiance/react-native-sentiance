pluginManagement {
  repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
  }
}

rootProject.name = 'RNSentiance'

dependencyResolutionManagement {
  repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
  repositories {
    google()
    mavenLocal()
    maven {
      url("$rootDir/../node_modules/react-native/android")
    }
    mavenCentral {
      // We don't want to fetch react-native from Maven Central as there are
      // older versions over there.
      content {
        excludeGroup "com.facebook.react"
      }
    }
    maven {
      url "https://repository.sentiance.com"
    }
  }
}

[
  'event-timeline',
  'test-common',
  'core',
  'crash-detection',
  'driving-insights',
  'user-context',
  'legacy',
  'smart-geofences',
].each { projectName ->
  def module = ":$projectName"
  include(module)
  project(module).projectDir = new File(rootProject.projectDir, "./$projectName/android")
}
