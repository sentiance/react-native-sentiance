plugins {
    id "com.android.library"
}

android {
    namespace "com.sentiance.react.bridge.core"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def isNewArchEnabled =
            (project.findProperty('newArchEnabled') ?: "false").toBoolean()

    defaultConfig {
        consumerProguardFiles 'rules-consumer.pro'
        buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchEnabled.toString()
    }
}

apply from: "$project.projectDir/package-json-reader.gradle"
apply from: "$project.projectDir/sentiance-version-finder.gradle"

def packageJson = PackageJson.of(project)
applyAndroidVersionsFrom(packageJson)
def sentianceSdkVersion = getSentianceSdkVersion()

dependencies {
    implementation(platform("com.sentiance:sdk-bom:${sentianceSdkVersion}"))
    api("com.sentiance:sdk") { transitive = true }

    if (findProject(':test-common')) {
        testImplementation project(':test-common')
    }
}

applyReactNativeDependency()
