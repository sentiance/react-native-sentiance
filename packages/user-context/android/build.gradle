import io.invertase.gradle.common.PackageJson

plugins {
  id "com.android.library"
  id "io.invertase.gradle.build" version "1.5"
}

def coreProj
if (findProject(':core')) {
  coreProj = project(':core')
} else if (findProject(':react-native-sentiance_core')) {
  // Starting from RN 0.61, the @ sign is stripped from project names
  coreProj = project(':react-native-sentiance_core')
} else if (findProject(':@react-native-sentiance_core')) {
  // On RN 0.60, the @ sign is not stripped from project names
  coreProj = project(':@react-native-sentiance_core')
} else {
  throw new GradleException('Could not find the react-native-sentiance/core package, have you installed it?')
}

def packageJson = PackageJson.getForProject(project)
def corePackageJson = PackageJson.getForProject(coreProj)

def defaultMinSdkVersion = corePackageJson['sdkVersions']['android']['minSdk']
def defaultTargetSdkVersion = corePackageJson['sdkVersions']['android']['targetSdk']
def defaultCompileSdkVersion = corePackageJson['sdkVersions']['android']['compileSdk']
def defaultBuildToolsVersion = corePackageJson['sdkVersions']['android']['buildTools']
def sentianceSdkVersion = corePackageJson['sdkVersions']['android']['sentiance']

// We can do this if we want to enforce strict version dependencies in-between Sentiance artifacts
def coreVersionDetected = corePackageJson['version']
def coreVersionRequired = packageJson['peerDependencies'][corePackageJson['name']]

if (coreVersionDetected != coreVersionRequired) {
  // Only log after build completed so log warning appears at the end
  gradle.buildFinished {
    project.logger.warn("ReactNativeSentianceUserContext WARNING: NPM package '${packageJson['name']}' depends on '${corePackageJson['name']}' v${coreVersionRequired} but found v${coreVersionDetected} instead. This might cause build issues or runtime crashes.")
  }
}

project.ext {
  set('react-native', [
    versions: [
      android: [
        minSdk    : defaultMinSdkVersion,
        targetSdk : defaultTargetSdkVersion,
        compileSdk: defaultCompileSdkVersion,
        buildTools: defaultBuildToolsVersion,
      ],
    ],
  ])
}

dependencies {
  api coreProj

  implementation 'com.facebook.react:react-native:+'
  api("com.sentiance:sdk:${sentianceSdkVersion}@aar") { transitive = true }
  api("com.sentiance:sdk-lifestyle:${sentianceSdkVersion}@aar") { transitive = true }
  api("com.sentiance:sdk-crash-detection:${sentianceSdkVersion}@aar") { transitive = true }
}

ReactNative.module.applyAndroidVersions()
